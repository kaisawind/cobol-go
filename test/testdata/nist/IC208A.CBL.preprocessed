      *> HEADER,COBOL,IC207A,SUBRTN,IC208A
       IDENTIFICATION DIVISION.
       PROGRAM-ID.
           IC208A.
      *> ***************************************************************
      *>                                                               *
      *>     VALIDATION FOR:-                                          *
      *>                                                               *
      *>     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
      *>                                                               *
      *>     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
      *>                                                               *
      *> ***************************************************************
      *>                                                               *
      *>       X-CARDS USED BY THIS PROGRAM ARE :-                     *
      *>                                                               *
      *>         X-55  - SYSTEM PRINTER NAME.                          *
      *>         X-82  - SOURCE COMPUTER NAME.                         *
      *>         X-83  - OBJECT COMPUTER NAME.                         *
      *>                                                               *
      *> ***************************************************************
      *>         THE SUBPROGRAM IC208 CONTAINS TABLES AND AN INDEX
      *>     DATA ITEM WHICH ARE DEFINED IN THE LINKAGE SECTION
      *>     AND NAMED AS OPERANDS IN THE USING PHRASE OF THE
      *>     PROCEDURE DIVISION HEADER.  ONE OF THE TABLES IS DEFINED
      *>     WITH AN OCCURS DEPENDING ON CLAUSE AND HAS CONDITION-NAME
      *>     ENTRIES ASSOCIATED WITH IT.  THE SEARCH STATEMENT IS USED
      *>     TO TEST THE VARIABLE LENGTH TABLE CAPABILITY.
      *> ***************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.
           XXXXX082.
       OBJECT-COMPUTER.
           XXXXX083.
       DATA DIVISION.
       LINKAGE SECTION.
       77  INDEX-1 USAGE IS INDEX.
       77  DN3 PICTURE 99.
       01  TABLE-01.
           02  DN1 PICTURE X
                   OCCURS 1 TO 15 TIMES
                   DEPENDING ON DN3
                   INDEXED BY IN1.
               88  CN1 VALUE "A".
               88  CN2 VALUE "H".
               88  CN3 VALUE "O".
       01  TABLE-02.
           02  DN2 PICTURE X
                   OCCURS 8 TIMES.
       PROCEDURE DIVISION USING TABLE-01, TABLE-02, INDEX-1, DN3.
       SECT-IC208-0001 SECTION.
       LINK-TEST-01.
      *>     THIS TEST USES THE INDEX DATA ITEM SET IN THE CALLING
      *>     PROGRAM TO SET AN INDEX AND REFERENCE A TABLE ITEM.
           SET IN1 TO INDEX-1.
           MOVE DN1 (IN1) TO DN2 (1).
      *>         LINK-TEST-02 TESTS THE VARIABLE LENGTH TABLE CAPABILITY
      *>     WITH THE DATA-NAME WHOSE CONTENTS IS THE TABLE LENGTH
      *>     DEFINED IN THE LINKAGE SECTION.
       LINK-TEST-02-01.
           SET IN1 TO 1.
           SEARCH DN1 VARYING IN1
               AT END MOVE "Z" TO DN2 (2)
               WHEN DN1 (IN1) EQUAL TO "D"
                   MOVE "D" TO DN2 (2).
       LINK-TEST-02-02.
           SET IN1 TO 1.
           SEARCH DN1 VARYING IN1
               AT END MOVE "Y" TO DN2 (3)
               WHEN DN1 (IN1) EQUAL TO "B"
                   MOVE "B" TO DN2 (3).
       LINK-TEST-02-03.
           MOVE 7 TO DN3.
           SET IN1 TO 1.
           SEARCH DN1 VARYING IN1
               AT END MOVE "X" TO DN2 (4)
               WHEN DN1 (IN1) EQUAL TO "H"
                   MOVE "H" TO DN2 (4).
       LINK-TEST-02-04.
           SET IN1 TO 1.
           SEARCH DN1 VARYING IN1
               AT END MOVE "W" TO DN2 (5)
               WHEN DN1 (IN1) EQUAL TO "G"
                   MOVE "G" TO DN2 (5).
       LINK-TEST-03.
           MOVE 10 TO DN3.
      *>         LINK-TEST-03 TESTS THE USE OF CONDITION-NAMES WHICH
      *>     WERE DEFINED IN THE LINKAGE SECTION.
       LINK-TEST-03-01.
           IF CN1 (1)  MOVE "A" TO DN2 (6).
       LINK-TEST-03-02.
           IF CN1 (5)  MOVE "N" TO DN2 (7)
               ELSE MOVE "V" TO DN2 (7).
       LINK-TEST-03-03.
           IF CN2 (8) MOVE "H" TO DN2 (8).
       LINK-TEST-04.
      *>         THIS TEST SETS THE INDEX DATA ITEM AND TABLE LENGTH
      *>     FOR REFERENCE IN THE CALLING PROGRAM.
           SET IN1 TO 2.
           SET INDEX-1 TO IN1.
           MOVE 9 TO DN3.
       EXIT-IC208.
           EXIT PROGRAM.
      *> END-OF,IC208A